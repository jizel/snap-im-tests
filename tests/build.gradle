import org.arquillian.spacelift.gradle.configuration.BuiltinConfigurationItemConverters
import travel.snapshot.qa.installation.MavenInstallation
import travel.snapshot.qa.DataPlatformTestOrchestration
import travel.snapshot.qa.installation.Docker
import travel.snapshot.qa.installation.DockerMachine
import travel.snapshot.qa.installation.DockerService
import travel.snapshot.qa.installation.GradleInstallation
import travel.snapshot.qa.installation.GitBasedInstallation
import travel.snapshot.qa.installation.TomcatInstallationResolver
import travel.snapshot.qa.test.DataPlatformTest
import travel.snapshot.qa.test.PlatformLifecycle
import travel.snapshot.qa.test.execution.api.APITestsExecution
import travel.snapshot.qa.test.execution.dataplatform.DataPlatformBuildExecutor
import travel.snapshot.qa.test.execution.dataplatform.DataPlatformModule
import travel.snapshot.qa.test.execution.dataplatform.DataPlatformModules
import travel.snapshot.qa.test.execution.db.mariadb.MariaDBDropper
import travel.snapshot.qa.test.execution.db.mariadb.MariaDBInitializer
import travel.snapshot.qa.test.execution.load.LoadTestsConfiguration
import travel.snapshot.qa.test.execution.load.LoadTestsExecution
import travel.snapshot.qa.test.execution.load.LoadTestsModuleParser
import travel.snapshot.qa.test.execution.load.LoadTestsSimulation
import travel.snapshot.qa.test.execution.load.LoadTestsSimulations
import travel.snapshot.qa.test.execution.log.DataPlatformLogReporter
import travel.snapshot.qa.test.execution.tomcat.DataPlatformDeployer
import travel.snapshot.qa.test.execution.tomcat.TomcatConfigurationDeployer
import travel.snapshot.qa.util.PropertyResolver

import static travel.snapshot.qa.docker.DockerServiceFactory.*

apply plugin: 'org.arquillian.spacelift'

spacelift {

    workspace new File(project.rootDir, 'snapshot/workspace')
    cacheDir new File(project.rootDir, 'snapshot/cache')

    final DataPlatformTestOrchestration platform = new DataPlatformTestOrchestration()

    configuration {
        dockerInstallations {
            converter BuiltinConfigurationItemConverters.getConverter(String[])
            value { ['docker', 'dockerMachine'] }
        }
        serviceInstallations {
            converter BuiltinConfigurationItemConverters.getConverter(String[])
            value { ['tomcat', 'mariadb', 'mongodb', 'activemq'] }
        }
        loadTestsInstallations {
            converter BuiltinConfigurationItemConverters.getConverter(String[])
            value { PropertyResolver.resolveLoadTestsInstallations() }
        }
        loadTestsTestExecutions {
            converter BuiltinConfigurationItemConverters.getConverter(String[])
            value { PropertyResolver.resolveLoadTestsTestExecutions() }
        }
        dockerImages {
            converter BuiltinConfigurationItemConverters.getConverter(String[])
            value {
                [
                        "docker.snapshot.travel:5000/snapshot/tomcat:8.0.32",
                        "docker.snapshot.travel:5000/snapshot/mariadb:10.1.11",
                        "docker.snapshot.travel:5000/snapshot/mongodb:3.2.1",
                        "docker.snapshot.travel:5000/snapshot/activemq:5.12.3"
                ]
            }
        }
        dockerMachine {
            value PropertyResolver.resolveDockerMachine()
        }
        dockerMachineMemorySize {
            value PropertyResolver.resolveDockerMachineMemorySize()
        }
        dockerMode {
            value PropertyResolver.resolveDockerMode()
        }
        connectionMode {
            value PropertyResolver.resolveConnectionMode()
        }
        testExecutionMode {
            value PropertyResolver.resolveTestExecutionMode()
        }
        dockerRegistryPassword {
            // you have to set systemProp.dockerRegistryPassword variable with Docker registry password in gradle.properties
            value PropertyResolver.resolveDockerRegistryPassword()
        }
        dataPlatformRepositories {
            converter BuiltinConfigurationItemConverters.getConverter(String[])
            value { ["dataPlatformRepository", "dataPlatformQARepository"] }
        }
        dataPlatformRepository {
            value PropertyResolver.resolveDataPlatformRepository()
        }
        dataPlatformQARepository {
            value PropertyResolver.resolveDataPlatformQARepository()
        }
        dataPlatformRepositoryCommit {
            value PropertyResolver.resolveDataPlatformRespositoryCommit("master")
        }
        dataPlatformRepositoryQACommit {
            value PropertyResolver.resolveDataPlatformRespositoryQACommit("master")
        }
        dataPlatformRepositoryCheckoutCommit {
            type Boolean
            value PropertyResolver.resolveDataPlatformRepositoryCheckoutCommit()
        }
        dataPlatformRepositoryQACheckoutCommit {
            type Boolean
            value PropertyResolver.resolveDataPlatformRepositoryQACheckoutCommit()
        }
        forceDataPlatformBuild {
            type Boolean
            value PropertyResolver.resolveForceDataPlatformBuild()
        }
        tomcatDeploymentStrategy {
            value PropertyResolver.resolveTomcatDeploymentStrategy()
        }
    }

    profiles {
        dataPlatform {
            enabledInstallations (dockerInstallations + serviceInstallations)
        }
        dataPlatformTest {
            enabledInstallations (dockerInstallations + serviceInstallations + dataPlatformRepositories + 'gradle')
        }
        platformStart(from: dataPlatform) {
            tests 'platformStart'
        }
        platformStop(from: dataPlatform) {
            tests 'platformStop'
        }
        platformInit(from: dataPlatformTest) {
            tests 'platformStart', 'platformInit'
        }
        platformBuild {
            enabledInstallations (dataPlatformRepositories + 'gradle')
            tests 'platformBuild'
        }
        apiTests(from: dataPlatformTest) {
            tests 'platformStart', 'platformInit', 'apiTests', 'platformStop'
        }
        loadTests {
            enabledInstallations loadTestsInstallations
            tests loadTestsTestExecutions
        }
    }

    installations {
        docker(from: Docker) {}
        dockerMachine(from: DockerMachine) {}

        tomcat(from: DockerService) {
            setup {
                new TomcatInstallationResolver(platform).resolve()
            }
        }
        mariadb(from: DockerService) {
            setup {
                platform.with(mariadb().init())
            }
        }
        mongodb(from: DockerService) {
            setup {
                platform.with(mongodb().init())
            }
        }
        activemq(from: DockerService) {
            setup {
                platform.with(activemq().init())
            }
        }

        maven(from: MavenInstallation) {}

        gradle(from: GradleInstallation) {}

        dataPlatformRepository(from: GitBasedInstallation) {
            repository "git@bitbucket.org:bbox/data-platform.git"
            commit dataPlatformRepositoryCommit
            checkoutCommit dataPlatformRepositoryCheckoutCommit
            home dataPlatformRepository
        }
        dataPlatformQARepository(from: GitBasedInstallation) {
            repository "git@bitbucket.org:bbox/dataplatformqa.git"
            commit dataPlatformRepositoryQACommit
            checkoutCommit dataPlatformRepositoryQACheckoutCommit
            home dataPlatformQARepository
        }
    }

    tests {

        // Platform lifecycles

        platformStart(from: PlatformLifecycle) {
            with platform
            init {
                new TomcatConfigurationDeployer(dockerMachine).deploy()
            }
            teardown false
        }
        platformStop(from: PlatformLifecycle) {
            with platform
            setup false
        }
        platformBuild(from: DataPlatformTest) {
            execute {
                new DataPlatformBuildExecutor().build(DataPlatformModule.PROJECT).execute(true)
            }
        }
        platformInit(from: DataPlatformTest) {
            execute {
                new MariaDBDropper(platform).drop(DataPlatformModules.DATABASE_SCHEMES).execute()
                new MariaDBInitializer(platform).init(DataPlatformModules.DATABASE_SCHEMES).execute()
            }
        }

        // reporting

        reportableTest(from: DataPlatformTest) {
            report {
                new DataPlatformLogReporter(platform, serviceInstallations).report()
            }
        }

        // API tests

        apiTests(inherits: reportableTest) {
            beforeTest {
                new DataPlatformBuildExecutor()
                        .build(DataPlatformModule.CONFIGURATION, DataPlatformModule.IDENTITY)
                        .execute(forceDataPlatformBuild)

                new DataPlatformDeployer(platform)
                        .deploy(DataPlatformModule.CONFIGURATION, DataPlatformModule.IDENTITY)
                        .execute()
            }
            execute {
                new APITestsExecution(platform).single("RunConfigurationTests").execute()
            }
        }

        // Load tests

        loadTests(inherits: reportableTest) {
            data {
                LoadTestsConfiguration.parse()
            }
            execute { List<LoadTestsSimulation, LoadTestsConfiguration> data ->
                new LoadTestsExecution(platform)
                        .simulation(data.get(0))
                        .configuration(data.get(1))
                        .execute()
            }
            afterTest {
                // save Gatling reports
            }
        }
        loadTestsDeployment {
            execute {
                new DataPlatformBuildExecutor().build(LoadTestsModuleParser.parse()).execute(forceDataPlatformBuild)

                new DataPlatformDeployer(platform).deploy(LoadTestsModuleParser.parse()).execute()
            }
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven {
            name 'snapshot'
            url 'https://nexus.snapshot.travel/content/groups/public'
            credentials {
                username nexusUser
                password nexusPassword
            }
        }
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath 'com.github.smiklosovic:arquillian-spacelift-gradle:1.0.2.Final'
        classpath 'travel.snapshot.qa.manager:docker-manager:1.0.3'
    }
}

task wrapper(type: Wrapper) { gradleVersion = '2.9' }
