import org.arquillian.spacelift.gradle.configuration.BuiltinConfigurationItemConverters
import org.arquillian.spacelift.gradle.git.GitBasedInstallation
import org.arquillian.spacelift.gradle.gradle.GradleInstallation
import travel.snapshot.qa.DataPlatformTestOrchestration
import travel.snapshot.qa.installation.DockerMachine
import travel.snapshot.qa.installation.DockerService
import travel.snapshot.qa.installation.Docker
import travel.snapshot.qa.test.DataPlatformTest
import travel.snapshot.qa.test.execution.dataplatform.DataPlatformBuildExecutor
import travel.snapshot.qa.test.execution.dataplatform.DataPlatformModule
import travel.snapshot.qa.test.execution.db.mariadb.MariaDBInitializer
import travel.snapshot.qa.test.execution.tomcat.DataPlatformDeployer
import travel.snapshot.qa.test.execution.tomcat.TomcatConfigurationDeployer
import travel.snapshot.qa.util.PropertyResolver

import static travel.snapshot.qa.docker.DockerServiceFactory.*

apply plugin: 'org.arquillian.spacelift'

spacelift {

    workspace new File(project.rootDir, 'snapshot/workspace')
    cacheDir new File(project.rootDir, 'snapshot/cache')

    final DataPlatformTestOrchestration platform = new DataPlatformTestOrchestration()

    configuration {
        dockerInstallations {
            converter BuiltinConfigurationItemConverters.getConverter(String[])
            value { ['docker', 'dockerMachine'] }
        }
        serviceInstallations {
            converter BuiltinConfigurationItemConverters.getConverter(String[])
            value { ['tomcat', 'mariadb', 'mongodb', 'activemq'] }
        }
        dockerImages {
            converter BuiltinConfigurationItemConverters.getConverter(String[])
            value {
                [
                        "docker.snapshot.travel:5000/snapshot/tomcat:8.0.30",
                        "docker.snapshot.travel:5000/snapshot/mariadb:10.1.10",
                        "docker.snapshot.travel:5000/snapshot/mongo:3.2.1",
                        "docker.snapshot.travel:5000/snapshot/activemq:5.12.1"
                ]
            }
        }
        dockerMachine {
            value PropertyResolver.resolveDockerMachine()
        }
        dockerMachineMemorySize {
            value PropertyResolver.resolveDockerMachineMemorySize()
        }
        dockerMode {
            value PropertyResolver.resolveDockerMode()
        }
        connectionMode {
            value PropertyResolver.resolveConnectionMode()
        }
        dockerRegistryPassword {
            // this can be externalized when desired
            value PropertyResolver.resolveDockerRegistryPassword()
        }

        dataPlatformRepositories {
            converter BuiltinConfigurationItemConverters.getConverter(String[])
            value { [ "dataPlatformRepository", "dataPlatformQARepository" ] }
        }
        dataPlatformRepositoryCommit {
            value PropertyResolver.resolveDataPlatformRespositoryCommit("master")
        }
        dataPlatformQARepositoryCommit {
            value PropertyResolver.resolveDataPlatformQARespositoryCommit("master")
        }
        repositorySkipFetch {
            value PropertyResolver.resolveRepositoryFetchSkip()
        }
        forceDataPlatformBuild {
            type Boolean
            value PropertyResolver.resolveForceDataPlatformBuild()
        }
        tomcatSpringConfigDirectory {
            value PropertyResolver.resolveTomcatSpringConfigDirectory()
        }
        tomcatDeploymentStrategy {
            value PropertyResolver.resolveTomcatDeploymentStrategy().toString()
        }
    }

    profiles {
        dataPlatform {
            enabledInstallations (dockerInstallations + serviceInstallations)
        }
        dataPlatformTest {
            enabledInstallations (dockerInstallations + serviceInstallations + dataPlatformRepositories + 'gradle')
        }
        platformStart(from: dataPlatform) {
            tests 'platformStart'
        }
        platformStop(from: dataPlatform) {
            tests 'platformStop'
        }
        platformBuild {
            enabledInstallations (dataPlatformRepositories + 'gradle')
            tests 'platformBuild'
        }
        apiTests(from: dataPlatformTest) {
            tests 'apiTests'
        }
    }

    installations {
        docker(from: Docker) {}
        dockerMachine(from: DockerMachine) {}

        tomcat(from: DockerService) {
            setup {
                platform.with(tomcat().init())
            }
        }
        mariadb(from: DockerService) {
            setup {
                platform.with(mariadb().init())
            }
        }
        mongodb(from: DockerService) {
            setup {
                platform.with(mongodb().init())
            }
        }
        activemq(from: DockerService) {
            setup {
                platform.with(activemq().init())
            }
        }

        gradle(from: GradleInstallation) {}

        dataPlatformRepository(from: GitBasedInstallation) {
            repository "git@bitbucket.org:bbox/data-platform.git"
            commit dataPlatformRepositoryCommit
            home "data-platform"
            commit dataPlatformRepositoryCommit
            skipFetch repositorySkipFetch
        }
        dataPlatformQARepository(from: GitBasedInstallation) {
            repository "git@bitbucket.org:bbox/dataplatformqa.git"
            commit dataPlatformQARepositoryCommit
            home dataPlatformQARepositoryCommit
            skipFetch repositorySkipFetch
        }
    }

    tests {
        platformStart(from: DataPlatformTest) {
            with platform
            teardown false
        }
        platformStop(from: DataPlatformTest) {
            with platform
            setup false
        }
        platformBuild(from: DataPlatformTest) {
            with platform
            execute {
                new DataPlatformBuildExecutor(workspace).build(DataPlatformModule.PROJECT).execute(true)
            }
        }
        apiTests(from: DataPlatformTest) {
            with platform
            init {
                new TomcatConfigurationDeployer(dockerMachine).deploy()
            }
            beforeTest {
                new DataPlatformBuildExecutor(workspace)
                        .build(DataPlatformModule.CONFIGURATION, DataPlatformModule.IDENTITY)
                        .execute(forceDataPlatformBuild)

                new MariaDBInitializer(workspace, platform)
                        .init(DataPlatformModule.IDENTITY)
                        .execute()

                new DataPlatformDeployer(workspace, platform)
                        .deploy(DataPlatformModule.CONFIGURATION, DataPlatformModule.IDENTITY).execute()
            }
            execute {
            }
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven {
            name 'snapshot'
            url 'https://nexus.snapshot.travel/content/groups/public'
            credentials {
                username nexusUser
                password nexusPassword
            }
        }
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath 'com.github.smiklosovic:arquillian-spacelift-gradle:1.0.0.Final'
        classpath 'travel.snapshot.qa.manager:docker-manager:1.0.0'
    }
}

task wrapper(type: Wrapper) { gradleVersion = '2.9' }
